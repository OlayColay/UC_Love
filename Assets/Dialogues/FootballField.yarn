title: Field
tags: 
position: -88,-7
---
//Interaction Settings
//Stan 
<<set $TalkStan to false>>
<<set $GiftStan to false>>
<<set $AskStan to false>>

//Background Settings
<<set $TimeOfDayField to GetTimeOfDay()>>
<<if $TimeOfDayField == 0>>
    <<SetBackground Backgrounds/Field/Morning>>
    //Sets background as Field Morning if it is Daytime
<<elseif $TimeOfDayField == 1>>
    <<SetBackground Backgrounds/Field/Noon>>
    //Sets background as Field Afternoon if it is Afternoon
<<else>>
    <<SetBackground Backgrounds/Field/Night>>
    //Sets background as Field Night if it Evening
<<endif>>
<<Fade true>>

//Flavor Text
<<set $FlavorTextField = Dice(5)>>
<<if $TimeOfDayField == 0>>
    <<if $FlavorTextField == 1>>
        Player: The air is pretty crisp and cool this morning.
        Player: Today's weather is great for a stroll. 
    <<elseif $FlavorTextField == 2>>
        Player: The smell of morning dew on the freshly mowed grass is always so refreshing. 
    <<elseif $FlavorTextField == 3>>
        Player: It sure is nice and quiet here...
        Player: I wonder if there's anybody else here besides me.
    <<elseif $FlavorTextField == 4>>
        Player: They only let really good athletes onto here... I got pretty lucky it seems!
    <<else>>
        Player: There's a few athletes on the field this morning working out.
        Player: They seem really focused on their sport. 
    <<endif>>
<<elseif $TimeOfDayField == 1>>
    <<if $FlavorTextField == 1>>
        Player: The field is a little more crowded now that it's noon.
        Player: There's a few groups here playing intramural sports.
    <<elseif $FlavorTextField == 2>>
        Player: Looks like they're preparing the field for an upcoming game. 
        Player: It's always fun to watch the energy of the players. 
    <<elseif $FlavorTextField == 3>>
        Player: WOAH! That ball almost hit my head. 
        Player: I better watch where I'm walking around here. 
    <<elseif $FlavorTextField == 4>>
        Player: The field is so expansive... I wonder how players are able to run across it so quickly. 
    <<else>>
        Player: All the grass here is making my allergies go haywire! 
    <<endif>>
<<else>>
    <<if $FlavorTextField == 1>>
        Player: The field is mostly empty at this hour except for the football team. 
        Player: It's kind of hypnotizing watching them practice. 
    <<elseif $FlavorTextField == 2>>
        Player: Sometimes I want to sit here and just meditate...
        Player: With how wet the ground is though, I'm worried it'll look like I peed my pants. 
    <<elseif $FlavorTextField == 3>>
        Player: It's a little chilly, but I guess it's good weather for if you're practicing a sport. 
        Player: The athletes practicing here are really working up a sweat. 
    <<elseif $FlavorTextField == 4>>
        Player: The athletes' uniforms really highlight their physiques. 
        Player: I wonder how much they have to work out to get to that stage.
    <<else>>
        Player: Looks like a game is about to start. The stands are packed!
    <<endif>>
<<endif>>
Player: Hmm..what should I do?
-> Meet someone
    <<jump Stan>>
-> Leave
    <<LoadScene MapScene>>
===
title: Stan
position: 153,-9
---
<<set $StanIntro to LoadInt("Stan_Intro")>>
<<SetSprite Middle Stan/Idle>>
<<SetActiveChar Middle>>
Player: What should I do?
-> Talk to Stan <<if $TalkStan == false>>
    <<set $TalkStan to true>>
    <<AddRelationshipScore Stan 10>>
    <<set $Stan_Dialogue = Dice(5)>>
    <<if $StanIntroLibrary != 1>>
        Stan: INTRO TEXT HERE
        <<SaveInt "Stan_Intro" 1>>
    <<elseif GetRelationshipScore("Stan") < 100>> //Strangers
        <<if $Stan_Dialogue == 1>>
            Stan: I'm busy.
        <<elseif $Stan_Dialogue == 2>>
            Stan: Don't you have some studying to do?
        <<elseif $Stan_Dialogue == 3>>
            Stan: Are you looking for Ellie?
            -> Yeah! Have you seen her around?
                <<SetSprite Middle Stan/Angry>>
                <<Shake Middle>>
                <<AddRelationshipScore Stan -20>>
                Stan: Ugh... BUG OFF and look for her yourself!
                <<SetSprite Middle Stan/sad>>
                Stan: sigh...
                <<SetSprite Middle Stan/Idle>>
            -> No, I was just heading to the library to study!
                <<SetSprite Middle Stan/om>>
                Stan: Ah! Sorry... people usually ask me where my sister Ellie is and it gets... really annoying.
                <<SetSprite Middle Stan/sad>>
                Stan: I've been a little on edge... sorry. 
                <<SetSprite Middle Stan/om>>
                Stan: What subject are you studying for today?
                -> Computer Science!
                    <<AddRelationshipScore Stan 10>>
                    <<SetSprite Middle Stan/lbcm>>
                    Stan: Oh! I'm a computer science major!
                    Stan: Well... a computer science and political science double major actually...science squared?
                    <<SetSprite Middle Stan/sad>>
                    Stan: Sorry... that was kind of a lame joke. 
                    <<SetSprite Middle Stan/lbom>>
                    Stan: Um... 
                    Stan: Ah... nevermind... sorry...
                -> Chemical Engineering!
                    <<SetSprite Middle Stan/om>>
                    Stan: Oh! Cool. Um... good luck studying.
                    <<SetSprite Middle Stan/Idle>>
                -> Psychology!
                    <<SetSprite Middle Stan/om>>
                    Stan: Oh! Cool. Um... good luck studying.
                    <<SetSprite Middle Stan/Idle>>
                -> Political Science!
                    <<AddRelationshipScore Stan 10>>
                    <<SetSprite Middle Stan/lbcm>>
                    Stan: Oh! I'm a political science major!
                    Stan: Well... a computer science and political science major actually...science squared?
                    <<SetSprite Middle Stan/sad>>
                    Stan: Sorry... that was kind of a lame joke. 
                    <<SetSprite Middle Stan/lbom>>
                    Stan: Um... if you ever need help...
                    <<SetSprite Middle Stan/sad>>
                    Stan: ... ah nevermind...sorry...
                    <<SetSprite Middle Stan/Idle>>
            -> No, I was looking for you!
                <<SetSprite Middle Stan/lbom>>
                <<Shake Middle>>
                Stan: Oh!
                Stan: Ah... sorry... people usually ask me where my sister Ellie is and it gets... really annoying. 
                <<SetSprite Middle Stan/Idle>>
                Stan: ...I have to get back to studying though, sorry.
        <<elseif $Stan_Dialogue == 4>> 
            Stan: Ah sorry, I have an assignment due in bit...
        <<else>> 
            Stan: Sorry, I have a test I have to study for today.
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 100 && GetRelationshipScore("Stan") < 200>> //Acquaintances
        <<if $Stan_Dialogue == 1>>
            Stan: You seem to come here often.
            -> Yeah, it's one of my favorite study spots!
                <<AddRelationshipScore Stan 10>>
                <<SetSprite Middle Stan/lbcm>>
                Stan: Oh! It's my favorite study spot too.
            -> Yeah, it's a nice place to talk to friends in!
                <<AddRelationshipScore Stan -20>>
                <<SetSprite Middle Stan/Angry>>
                Stan: Sigh... You do realize that people need to work here, right?
                Stan: Ugh...
                <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 2>>
            Stan: Ugh... my skin has been so dry lately...
            Stan: Staying up late studying and drinking all this coffee is really messing with my skin.
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/Angry>>
            Stan: Whatever you do, don't take Political Science 124. It's one of the worst classes I've taken here.
            <<Shake Middle>>
            Stan: International political economy. Never again. 
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 4>> 
            Stan: I have three ten-page papers and a giant coding assignment due this week.
            Stan: sigh... 
        <<else>> 
            Stan: I think I got two hours of sleep last night.
            Stan: Have you tried the campus cafe yet?
            Stan: The black coffee there is pretty strong and the lines are never too bad.
            Stan: I usually like to stop by in the mornings before spending the rest of my day at the library.
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 200 && GetRelationshipScore("Stan") < 300>> //Friends
        <<if $Stan_Dialogue == 1>>
            Stan: Sometimes when I'm working late at night, my sister will go into my room and nag at me to go to sleep.
            Stan: It's really annoying... can't she see that I have work to do? Not all of us can spend our days lolligagging...
            -> Yeah, that is really annoying of her...
                Stan: Yeah...
                <<SetSprite Middle Stan/sad>>
                Stan: I know she probably does it because she worries about my health though...
                Stan: I feel kind of bad complaining about her or taking her for granted... maybe I should appreciate her more. 
                <<SetSprite Middle Stan/Idle>>
            -> Maybe she's worried about you...
                <<SetSprite Middle Stan/om>>
                <<AddRelationshipScore Stan 10>>
                Stan: ...oh... I never thought about it that way....
                Stan: I think that she does care a lot about me... I feel guilty because I've been pushing her away...
                Stan: I've really been taking her for granted... I definitely should appreciate her more.
                Stan: I think I'll buy her one of the new bear plushies she loves so much as an apology...
        <<elseif $Stan_Dialogue == 2>>
            Stan: Could I get your opinion on something? Be honest, do I look intimidating?
            -> Yeah, a little!
                <<AddRelationshipScore Stan 10>>
                Stan: Yeah... I think so too. I do kind of have a grumpy face... my parents say this a lot too...
                Stan: To be honest, it was kind of rough growing up with my sister. 
                Stan: I love my sister, but everyone would always compare me to her and it really cuts deep after a while.
                Stan: I started comparing myself a lot to her too and it has caused a rift between us. 
                <<SetSprite Middle Stan/sad>>
                Stan: It's something that I still struggle with a lot.
                Stan: ... She's a lot more cheery and sociable than I am... Our aunties would call me "raincloud" and call her "sunshine."
                <<SetSprite Middle Stan/lbom>>
                Stan: Ah! Sorry... I'm rambling again. 
                Stan: Thank you for being honest with me though. It makes me feel like I can trust you a lot more.
                <<SetSprite Middle Stan/lbcm>>
                    -> Of course!
            -> No, not at all!
                Stan: I find that hard to believe. 
        <<elseif $Stan_Dialogue == 3>>
            Stan: I've been getting a lot of ads for cute "ClickClock" outfits... but they're pretty different from what I usually wear.
            Stan: I want to try changing up what I wear... but I get really self-conscious about how I'll look...
            Stan: I feel like other styles will look weird on me... I'd probably look like a fool.
            -> You should try something new! I'm sure that you'll look great!
                <<AddRelationshipScore Stan 10>>
                <<SetSprite Middle Stan/lbom>>
                Stan: Thank you for the encouragement... it means a lot...
                Stan: I think it will be a while before I'm ready to wear anything new outside though.
                <<SetSprite Middle Stan/lbcm>>
                Stan: I think I'll ask my sister if I can borrow some of her clothes!
                <<SetSprite Middle Stan/Idle>>
            -> I don't think that the "ClickClock" style would suit you...
                <<SetSprite Middle Stan/sad>>
                Stan: Yeah... who am I kidding... I don't think I could ever be anything other than what I am right now.
                Stan: Thanks for the reality check.
                <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 4>> 
            Stan: If you have room in your schedule, you should take the Game Theory course here.
            Stan: I found it really interesting and it's pretty easy if you do the problem sets and pay attention to lecture.
            <<SetSprite Middle Stan/lbcm>>
            Stan: If you take it, let me know and I'll send my notes over to you!
        <<else>> 
            Stan: I'd really like to see the sunset in Los Angeles one day... I don't really go outside that often. 
            Stan: I wish I had more time to do things that I care about or more time to take care of myself.
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 300 && GetRelationshipScore("Stan") < 400>> //Crushing
        <<if $Stan_Dialogue == 1>>
            Stan: I can't believe that midterm season is almost here. 
            <<SetSprite Middle Stan/lbom>>
            Stan: Do you need a place to study? I have an extra seat at my table... if you want.
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 2>>
            Stan: I wish I had a little more time to spend on my hobbies.
            <<SetSprite Middle Stan/sad>>
            Stan: I really enjoy playing chess, but I've been so swamped with schoolwork that I barely have time to take care of myself.
            Stan: Lately, my little sister Ellie started doing better academically than I did... and my parents have been pushing me to catch up...
            Stan: I'm putting so much pressure on myself, and I feel like I'm going to break...
            Stan: I'm so burnt out, but I feel like I have to keep going... I barely even have time to eat these days...
            -> School is tough, but you can hang in there! 
                Stan: ... I guess so... I'm really not feeling well though...
            -> I know that school is really important to you, but your mental and physical health matter too!
                Stan: I know... I should be taking better care of myself.
            -> How can I support you?
                <<SetSprite Middle Stan/lbom>>
                Stan: Hmm? ...Support? Ah you don't have to do anything! I don't want to trouble you!
                -> Insist on helping Stan
                    <<AddRelationshipScore Stan 10>>
                    Player: No, no! I insist. You mean a lot to me. I want you to take care of your health, but I know that your goals mean a lot to you.
                    Player: So I want to do what I can to help you reach your goals!
                    <<SetSprite Middle Stan/hbom>>
                    Stan: I...
                    <<SetSprite Middle Stan/lbom>>
                    Player: What if I brought you a sandwich from the dining hall while you're studying?
                    Player: That can save you some time and make sure that you still get a healthy meal.
                    <<SetSprite Middle Stan/hbcm>>
                    Stan: I'd... I'd really appreciate that. Thank you. 
                -> Drop the subject
                    Player: Oh... ok!
                    <<SetSprite Middle Stan/sad>>
                    Stan: I'm fine on my own... I promise...
        <<elseif $Stan_Dialogue == 3>>
            Stan: I was analyzing some love stories for one of my classes... and I don't know why but I thought this one line was really pretty.
            <<SetSprite Middle Stan/lbom>>
            Stan: It was "Love is just a word until someone comes along and gives it meaning."
            <<SetSprite Middle Stan/hbom>>
            Stan: Ah! Sorry! For a moment it sounded like I was saying that sappy of a line to you...
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 4>> 
            Stan: Sometimes I forget to drink my coffee and I get really tired.
            <<SetSprite Middle Stan/lbom>>
            Stan: ...I feel a lot less tired when I'm around you though...
            <<Shake Middle>>
            Stan: ... which is weird because I think that violates the Law of Conservation!
            <<SetSprite Middle Stan/Idle>>
        <<else>> 
            Stan: Are you taking the class with Professor BearBox next quarter?
            <<SetSprite Middle Stan/Angry>>
            Stan: I heard that he assigns a lot of group projects and that he's a pretty harsh grader. I really hate group projects...
            <<SetSprite Middle Stan/lbom>>
            Stan: If you take the class, we could work on the projects together... if you want...
            <<SetSprite Middle Stan/lbcm>>
            Stan: I think that you'd make a good partner...
        <<endif>>    
    <<else>> //Lovers
        <<if $Stan_Dialogue == 1>>
            Stan: I dropped this one computer science class that was really stressing me out.
            <<SetSprite Middle Stan/Angry>>
            Stan: The workload was excruciatingly heavy and the professor wasn't very understanding.
            <<SetSprite Middle Stan/sad>>
            Stan: I had a lot on my plate... and I've decided that I want to start prioritizing my health a little more!
            Stan: I still struggle with feeling a little guilty about taking it easier on myself, but I try to remind myself to be patient with myself.
            <<SetSprite Middle Stan/lbcm>>
            Stan: I have a bit more time to spend on things I actually care about like the chess club now that I'm not wasting it on obsolete assignments.
            Stan: I'm really thankful for my loved ones... my sister has been really supportive of me even though we've been a bit distant these past few years.
            <<SetSprite Middle Stan/hbom>>
            Stan: You've been really supportive of me too...
            <<SetSprite Middle Stan/hbom>>
            Stan: Thank you.
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/lbcm>>
            Stan: I've been experimenting a bit more with fashion lately.
            Stan: I found some vintage outfits at a thrift store and sometimes I put it on to wear around the house.
            Stan: I still feel a little shy about wearing something deviating from my usual style around on campus... but it feels really fun to not limit my wardrobe to pratical academic-wear.
            Stan: I feel a lot more free.
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/lbcm>>
            Stan: My sister and I have been getting a little closer lately. 
            <<SetSprite Middle Stan/sad>>
            Stan: We were best friends as kids and used to do everything together... but we drifted apart as we grew older.
            Stan: I think that growing up in such a competitive environment pit us against each other and wedged us apart.
            <<SetSprite Middle Stan/lbcm>>
            Stan: This weekend we went out for a girls' day brunch together. Next weekend, we're going to go thrifting together too!
            Stan: All the advice you gave me really helped me patch my relationship with my sister.
        <<elseif $Stan_Dialogue == 4>> 
            <<SetSprite Middle Stan/lbom>>
            Stan: I heard that there's a rooftop nearby that we can climb. I heard that the sunset there is really pretty and romantic.
            <<SetSprite Middle Stan/lbcm>>
            Stan: I'd really love to go with you sometime... I can make some time!
            <<SetSprite Middle Stan/Idle>>
        <<else>> 
            <<SetSprite Middle Stan/lbcm>>
            Stan: I've been feeling a lot better overall lately... I think that self-care has really improved my mental and physical health.
            Stan: After a long day, I try to make time to do some yoga with my sister, cook a healthy meal, and do a bit of skincare. 
            Stan: I have this really great soondubu recipe that you should try sometime. 
            <<SetSprite Middle Stan/Idle>>
        <<endif>>   
    <<endif>>
    <<jump StanLibrary>>
-> Give Gift <<if $GiftStan == false && (GetInventoryLength(false) > 0 || GetInventoryLength(true) > 0)>>
    <<OpenInventory>>
    <<if GetInventoryCanceled() == true>>
        <<jump StanLibrary>>
    <<endif>> 
    <<set $GiftStan = true>>
    <<set $GiftScoreK = GetItemScore("Stan")>>
    <<AddRelationshipScore Stan {$GiftScoreK}>>
    <<if $GiftScoreK == 20>>
        //Loved Gift
        <<SetSprite Middle Stan/hbom>>
        Stan: H-huh? T-thank you! How'd you know that I really love these...
        <<SetSprite Middle Stan/Idle>>
    <<elseif $GiftScoreK == 10>>
        //Liked Gift
        <<SetSprite Middle Stan/lbcm>>
        Stan: Oh! Uh… thanks!
        <<SetSprite Middle Stan/Idle>>
    <<elseif $GiftScoreK == -10>>
        //Hated Gift
        <<SetSprite Middle Stan/Angry>>
        Stan: Is this a joke?
        <<SetSprite Middle Stan/Idle>>
    <<elseif $GiftScoreK == 40>>
        <<SetSprite Middle Stan/lbom>>
        Stan: Is that... my diary??!
        Stan: Ummm... I'm not sure how you got this... I thought I lost it a long time ago!
        <<SetSprite Middle Stan/angry>>
        <<Shake Middle>>
        Stan: You didn't look through it right?!
        <<SetSprite Middle Stan/lbom>>
        Stan: Sorry... it's just a little embarassing...
        Stan: It sounds kind of silly, but I used to journal a lot, especially when I was stressed.
        Stan: I used it as a stress outlet, so the diary is really personal to me...
        <<SetSprite Middle Stan/lbcm>>
        Stan: I think I'll start writing again! Thank you!
        <<SetSprite Middle Stan/Idle>>
    <<else>>
        // Not their key item gift
        Stan: Oh! This looks important, but it's not mine...
        <<if $GiftScoreK == 0>> // Ellie's
            <<AddItem 40 0 0 0 0 0 "Gifts/Cookbook" "Cookbook" true>>
        <<elseif $GiftScoreK == 1>> // Stan's
            <<AddRelationshipScore Stan -1>>
            <<AddItem 1 40 1 1 1 1 "Gifts/Diary" "Diary" true>>
        <<elseif $GiftScoreK == 2>> // Santana's
            <<AddRelationshipScore Stan -2>>
            <<AddItem 2 2 40 2 2 2 "Gifts/Spade" "Spade" true>>
        <<elseif $GiftScoreK == 3>> // Riviera's
            <<AddRelationshipScore Stan -3>>
            <<AddItem 3 3 3 40 3 3 "Gifts/ToteBag" "ToteBag" true>>
        <<elseif $GiftScoreK == 4>> // Irene's
            <<AddRelationshipScore Stan -4>>
            <<AddItem 4 4 4 4 40 4 "Gifts/Keychain" "Keychain" true>>
        <<else>> // Tommy's
            <<AddRelationshipScore Stan -5>>
            <<AddItem 5 5 5 5 5 40 "Gifts/Watch" "Watch" true>>
        <<endif>>
    <<endif>>
    <<jump StanLibrary>>
-> Ask <<if $AskStan == false>>
    -> Ask on a date
        <<set $AskStan = true>>
        <<if GetRelationshipScore("Stan") >= 300>>
            <<AddRelationshipScore Stan 10>>
            <<SetSprite Middle Stan/lbom>>
            Stan: Oh! Um...let me check my schedule...
            <<SetSprite Middle Stan/lbcm>>
            Stan: I can make time for a date!
            <<Fade false>>
            <<ClearSprite Middle>>
            <<SetActiveChar Date>>
            <<SetBackground Backgrounds/Date>>
            <<SetSprite Bottom Backgrounds/Table>>
            <<SetSprite Date Stan/lbcm>>
            <<Fade true>>
            <<DateMinigame>>
            <<Wait 4>>
            Stan: I really needed a break from studying...
            Stan: ...Thank you for taking me out!
            <<Fade false>>
            <<ClearSprite Bottom>>
            <<ClearSprite Date>>
            <<if $OptionalPowellCat == 9>>
                <<if $TimeofDayLibrary == 0>>
                    <<SetBackground Backgrounds/Library/MorningCat>>
                <<elseif $TimeofDayLibrary == 1>>
                    <<SetBackground Backgrounds/Library/NoonCat>>
                <<else>>
                    <<SetBackground Backgrounds/Library/NightCat>>
                <<endif>>
            <<else>>
                <<if $TimeofDayLibrary == 0>>
                    <<SetBackground Backgrounds/Library/Morning>>
                <<elseif $TimeofDayLibrary == 1>>
                    <<SetBackground Backgrounds/Library/Noon>>
                <<else>>
                    <<SetBackground Backgrounds/Library/Night>>
                <<endif>>
            <<endif>>
            <<SetSprite Middle Stan/lbcm>>
            <<SetActiveChar Middle>>
            <<Fade true>>
            Stan: I usually eat junk food or instant noodles... so thank you for the pasta!
            <<SetSprite Middle Stan/Idle>>
            <<jump StanLibrary>>
        <<else>>
            <<SetSprite Middle Stan/Question>>
            Stan: You? Me? A date together? 
            Stan: That's hilarious. 
            <<SetSprite Middle Stan/Laughing>>
            <<jump Stan>>
        <<endif>>
    -> Ask to the dance
        <<set $AskStan = true>>
        <<if GetRelationshipScore("Stan") >= 400>>
            Player: Would you like to go to the dance with me?
            <<SetSprite Middle Stan/hbom>>
            Stan: Y-yes! I'd love to!
            <<set $DancePartner = 0>>
            <<jump Dance>>
        <<else>>
            <<SetSprite Middle Stan/Question>>
            Stan: You? Me? That dinky old dance?
            <<SetSprite Middle Stan/Pitiful>>
            Stan: I respect your little attempt to swindle me into your little fundraiser project, but I'd never be caught dead stepping foot into there. 
            Stan: With YOU nonetheless!
            <<SetSprite Middle Stan/Laughing>>
            Stan: That's the most hilarious thing anyone has said to me this century!
            <<SetSprite Middle Stan/Passive>>
            <<AddRelationshipScore Stan -20>>
            <<jump Stan>>
        <<endif>>
    -> Cancel
        <<jump Stan>>
-> Leave
    <<UpdateTimeOfDay>>
    <<LoadScene MapScene>>
===