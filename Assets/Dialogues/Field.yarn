title: Field
tags: 
position: -88,-7
---
//Interaction Settings
//Stan 
<<set $TalkStan to false>>
<<set $GiftStan to false>>
<<set $AskStan to false>>

//Background Settings
<<set $TimeOfDayField to GetTimeOfDay()>>
<<if $TimeOfDayField == 0>>
    <<SetBackground Backgrounds/Field/Morning>>
    //Sets background as Field Morning if it is Daytime
<<elseif $TimeOfDayField == 1>>
    <<SetBackground Backgrounds/Field/Noon>>
    //Sets background as Field Afternoon if it is Afternoon
<<else>>
    <<SetBackground Backgrounds/Field/Night>>
    //Sets background as Field Night if it Evening
<<endif>>
<<Fade true>>

<<set $FieldPermissions = LoadInt("Field")>>
<<if $FieldPermissions == 0>>
    Player: Oh, only pro-atheletes are allowed on here. 
    Player: I better go before security tackles me. 
    Player: Maybe if got some mega gainz at the gym I could use this field...
    -> Leave
        <<LoadScene MapScene>>
<<endif>>
//Flavor Text
<<set $FlavorTextField = Dice(5)>>
<<if $TimeOfDayField == 0>>
    <<if $FlavorTextField == 1>>
        Player: The air is pretty crisp and cool this morning.
        Player: Today's weather is great for a stroll. 
    <<elseif $FlavorTextField == 2>>
        Player: The smell of morning dew on the freshly mowed grass is always so refreshing. 
    <<elseif $FlavorTextField == 3>>
        Player: It sure is nice and quiet here...
        Player: I wonder if there's anybody else here besides me.
    <<elseif $FlavorTextField == 4>>
        Player: They only let really good athletes onto here... I got pretty lucky it seems!
    <<else>>
        Player: There's a few athletes on the field this morning working out.
        Player: They seem really focused on their sport. 
    <<endif>>
<<elseif $TimeOfDayField == 1>>
    <<if $FlavorTextField == 1>>
        Player: The field is a little more crowded now that it's noon.
        Player: There's a few groups here playing intramural sports.
    <<elseif $FlavorTextField == 2>>
        Player: Looks like they're preparing the field for an upcoming game. 
        Player: It's always fun to watch the energy of the players. 
    <<elseif $FlavorTextField == 3>>
        Player: WOAH! That ball almost hit my head. 
        Player: I better watch where I'm walking around here. 
    <<elseif $FlavorTextField == 4>>
        Player: The field is so expansive... I wonder how players are able to run across it so quickly. 
    <<else>>
        Player: All the grass here is making my allergies go haywire! 
    <<endif>>
<<else>>
    <<if $FlavorTextField == 1>>
        Player: The field is mostly empty at this hour except for the football team. 
        Player: It's kind of hypnotizing watching them practice. 
    <<elseif $FlavorTextField == 2>>
        Player: Sometimes I want to sit here and just meditate...
        Player: With how wet the ground is though, I'm worried it'll look like I peed my pants. 
    <<elseif $FlavorTextField == 3>>
        Player: It's a little chilly, but I guess it's good weather for if you're practicing a sport. 
        Player: The athletes practicing here are really working up a sweat. 
    <<elseif $FlavorTextField == 4>>
        Player: The athletes' uniforms really highlight their physiques. 
        Player: I wonder how much they have to work out to get to that stage.
    <<else>>
        Player: Looks like a game is about to start. The stands are packed!
    <<endif>>
<<endif>>
Player: Hmm..what should I do?
-> Meet someone
    <<jump Stan>>
-> Leave
    <<LoadScene MapScene>>
===
title: Stan
position: 153,-9
---
<<set $StanIntro to LoadInt("Stan_Intro")>>
<<SetSprite Middle Stan/Smiling>>
<<SetActiveChar Middle>>
Player: What should I do?
-> Talk to Stan <<if $TalkStan == false>>
    <<set $TalkStan to true>>
    <<AddRelationshipScore Stan 15>>
    <<set $Stan_Dialogue = Dice(5)>>
    <<if $StanIntro != 1>>
        Stan: Well if it isn't my favorite UC Loser... Did you come here looking for my attention?
        Stan: I'm surprised they allowed you onto this field. Usually it's reserved for the best athletes. 
        <<SetSprite Middle Stan/Laughing>>
        Stan: You could barely put up a half-decent fight back at the plaza. 
        <<SetSprite Middle Stan/Passive>>
        Stan: Still... I have to give you some credit for getting onto here. Coach is super picky about who he lets onto this field.
        <<SetSprite Middle Stan/Giggle>>
        Stan: Not even Tommy levels of money could bribe him into letting you onto the field.
        <<SetSprite Middle Stan/Grin>>
        Stan: Maybe he saw some potential in you. I guess we're sharing a practice field now. 
        <<SetSprite Middle Stan/Pitiful>>
        Stan: Still, don't expect me to be all buddy buddy with you. We are on opposite teams after all. 
        <<SetSprite Middle Stan/Grin>>
        Stan: Keep coming to field practice and maybe I'll pay some attention to you. 
        <<SetSprite Middle Stan/Unhinged2>>
        Stan: After all, I want a worthy opponent to pummel! 
        <<SetSprite Middle Stan/Passive>>
        <<SaveInt "Stan_Intro" 1>>
    <<elseif GetRelationshipScore("Stan") < 100>> //Strangers
        <<if $Stan_Dialogue == 1>>
            <<SetSprite Middle Stan/Question>>
            Stan: Why are you following me around like a stray puppy? 
            <<SetSprite Middle Stan/Pitiful>>
            Stan: Get lost. 
            <<SetSprite Middle Stan/Passive>>
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/Irritated>>
            Stan: If you keep standing in the middle of the field, you're going to get hit by a ball. 
            <<SetSprite Middle Stan/Passive>>
        <<elseif $Stan_Dialogue == 3>>
            Stan: Oh you're working out? Do you need a spotter?
            -> Yes
                <<SetSprite Middle Stan/Pitiful>>
                Stan: Sounds like your problem, not mine.
                <<SetSprite Middle Stan/Smiling>>
            -> No
                <<SetSprite Middle Stan/Pitiful>>
                Stan: With arms like those? I think you should reconsider.
                <<SetSprite Middle Stan/Passive>>
        <<elseif $Stan_Dialogue == 4>> 
            <<SetSprite Middle Stan/Unhinged2>>
            <<Shake Middle>>
            Stan: Hey shrimp, how fast do you think I can make you cry "uncle"?
            <<SetSprite Middle Stan/Passive>>
        <<else>> 
            <<SetSprite Middle Stan/Grin>>
            Stan: We used to have a tradition where the newbies would have to carry around their seniors' sports equipment for a couple months after they joined. 
            <<SetSprite Middle Stan/Laughing>>
            Stan: Coach recently got rid of that though, saying that it created a "hostile environment" or whatever.
            <<SetSprite Middle Stan/Pitiful>>
            Stan: What a shame... 
            <<SetSprite Middle Stan/Unhinged2>>
            <<Shake Middle>>
            Stan: I really would've liked to see you struggle under all that weight. 
            <<SetSprite Middle Stan/Smiling>>
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 100 && GetRelationshipScore("Stan") < 200>> //Acquaintances
        <<if $Stan_Dialogue == 1>>
            <<SetSprite Middle Stan/Irritated>>
            Stan: A lot of fangirls try to give me gifts thinking that it'd make me like them...
            Stan: Do they think they can buy my attention with cheap gifts or something?
            <<SetSprite Middle Stan/Passive>>
            Stan: I hate that. People you can trust are way more important than people that give you useless crap.
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/Laughing>>
            Stan: It's so funny to prank Tommy. He can be so gullible at times. 
            Stan: Yesterday, I told him that bottled water contains high levels of hydrogen dioxide and he started spitting out his water.
            -> Haha! Tommy's so stupid!
                <<SetSprite Middle Stan/Blackout>>
                <<Shake Middle>>
                Stan: Tommy might not be the sharpest tool in the shed, but he's my friend. 
                <<SetSprite Middle Stan/Furious2>>
                <<Shake Middle>>
                Stan: Don't talk about him like that. Got it? 
                <<SetSprite Middle Stan/Passive>>
                <<AddRelationshipScore Stan -10>>
            -> That's kind of mean... you shouldn't do that...
                <<SetSprite Middle Stan/Passive>>
                Stan: Tch. It's all in good fun. He pranks me back just as hard if not harder. 
                <<SetSprite Middle Stan/Laughing>>
                Stan: One time, he put this hentai sticker on the back of my car just before I drove off.
                <<SetSprite Middle Stan/Grin>>
                Stan: I was getting weird stares from other drivers all day and I couldn't figure out why until after I parked. 
                Stan: I admit, it was a pretty funny prank. 
                <<SetSprite Middle Stan/Question>>
                Stan: Though I have to wonder, why did he have that hentai sticker?
                <<SetSprite Middle Stan/Smiling>>
            -> Haha! That's a funny prank!
                <<SetSprite Middle Stan/Giggle>>
                Stan: Yeah, Tommy and I prank each other quite a bit. 
                Stan: We might seem a little harsh to each other from an outsider's perspective, but our friendship is just like that. 
                <<SetSprite Middle Stan/Smiling>>
                Stan: Tommy is one of my closest friends. I'm really grateful to have him in my life. 
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/Question>>
            Stan: I have to ask, why are you still hanging around me? 
            <<SetSprite Middle Stan/Passive>>
            Stan: We didn't exactly start off on the right foot, and it's not like I've been much better to you since then. 
            <<SetSprite Middle Stan/Question>>
            Stan: So then, may I ask why? Why are you still here? 
            -> You seem lonely...
                <<SetSprite Middle Stan/Pitiful>>
                Stan: Lonely? Tch... Me? Lonely? 
                Stan: I'm not lonely... I have Tommy and... 
                <<SetSprite Middle Stan/Question>>
                Stan: My studies and sports... and...
                <<SetSprite Middle Stan/Pitiful>>
                Stan: Look. I don't need many friends. I prefer to keep to myself, so bug off!
                <<SetSprite Middle Stan/Blackout>>
                <<Wait 1>>
                <<Shake Middle>>
                <<SetSprite Middle Stan/Passive>>
            -> I want to work out and get stronger! 
                <<SetSprite Middle Stan/Laughing>>
                Stan: Do you really expect me to believe that? 
                Stan: We're in a gigantic field, but somehow, coincidentially, you choose to practice right next to me every single time. 
                Stan: Why don't you be honest and tell me why you're following me like a puppy?
                -> You seem lonely...
                    <<SetSprite Middle Stan/Pitiful>>
                    Stan: Lonely? Tch... Me? Lonely? 
                    Stan: I'm not lonely... I have Tommy and... 
                    <<SetSprite Middle Stan/Question>>
                    Stan: My studies and sports... and...
                    <<SetSprite Middle Stan/Pitiful>>
                    Stan: Look. I don't need many friends. I prefer to keep to myself, so bug off!
                    <<SetSprite Middle Stan/Blackout>>
                    <<Wait 1>>
                    <<Shake Middle>>
                    <<SetSprite Middle Stan/Passive>>
                -> I have a humiliation kink
                    <<SetSprite Middle Stan/Blackout>>
                    <<Shake Middle>>
                    Stan: ...You what?
                    <<SetSprite Middle Stan/Pitiful>>
                    Stan: You have a humiliation kink? 
                    Stan: You're... joking right? 
                    <<SetSprite Middle Stan/Question>>
                    Stan: You're... not joking...
                    <<SetSprite Middle Stan/Sweating>>
                    <<Wait 1>>
                    <<SetSprite Middle Stan/Blackout>>
                    Stan: Go touch grass this instant. Now. 
                    <<SetSprite Middle Stan/Passive>> 
            -> I have a humiliation kink
                <<SetSprite Middle Stan/Blackout>>
                <<Shake Middle>>
                Stan: ...You what?
                <<SetSprite Middle Stan/Pitiful>>
                Stan: You have a humiliation kink? 
                Stan: You're... joking right? 
                <<SetSprite Middle Stan/Question>>
                Stan: You're... not joking...
                <<SetSprite Middle Stan/Sweating>>
                <<Wait 1>>
                <<SetSprite Middle Stan/Blackout>>
                Stan: Go touch grass this instant. Now. 
                <<SetSprite Middle Stan/Passive>> 
        <<elseif $Stan_Dialogue == 4>> 
            <<SetSprite Middle Stan/Giggle>>
            Stan: The field is nice isn't it? 
            <<SetSprite Middle Stan/Grin>>
            Stan: It's nice to have a little privacy when you practice away from fangirls.
            <<SetSprite Middle Stan/Laughing>>
            Stan: Though it is a bit of a shame that I have to share it with YOU now. 
            <<SetSprite Middle Stan/Question>>
            Stan: There are worse things in the world I guess. 
            <<SetSprite Middle Stan/Pitiful>>
            Stan: Don't get too comfortable here though.
        <<else>> 
            <<SetSprite Middle Stan/Question>>
            Stan: I wish coach didn't pass that ban on hazing right when you joined.
            <<SetSprite Middle Stan/Unhinged2>>
            Stan: I had so many good ideas for what to do to you...
            Stan: Wouldn't it be cute if I made you do my laundry? Or if I made you answer each command with "yes sir"?
            <<SetSprite Middle Stan/Question>>
            Stan: Coach said that if he heard of anyone hazing the newbies, he'd kick them off the team. 
            <<Shake Middle>>
            <<SetSprite Middle Stan/Blackout>>
            Stan: If there's one person that I don't want to piss off, it's him. 
            <<SetSprite Middle Stan/Pitiful>>
            Stan: Count yourself lucky... for now.
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 200 && GetRelationshipScore("Stan") < 300>> //Friends
        <<if $Stan_Dialogue == 1>>
            <<SetSprite Middle Stan/Question>>
            Stan: Hey, I noticed that your form for throws was a bit off during the previous practice. 
            Stan: Your fingers are too low on the ball and your throw trajectory is using the wrong muscles. 
            <<SetSprite Middle Stan/Irritated>>
            Stan: Ugh, it's kind of hard to explain. 
            <<SetSprite Middle Stan/Question>>
            Stan: Can you just give me your hand? 
            -> Yes 
                <<SetSprite Middle Stan/Smiling>>
                <<AddRelationshipScore Stan 10>>
                Stan: You want to make sure that your fingers grip the ball at this kind of an angle. Like this!
                <<SetSprite Middle Stan/Grin>>
                Stan: Interlace your finger with the grooves between the laces. This should give you a better grip that you can put more force into when you throw. 
                Stan: When you throw the ball, shift your weight from backwards to forwards. 
                <<SetSprite Middle Stan/Smiling>>
                Stan: Why don't you give it a quick try? 
                <<SetSprite Middle Stan/Grin>>
                Stan: See? Now your throws are both faster and further! 
                Stan: Keep at it and maybe one day you'd be able to keep up with me!
                <<SetSprite Middle Stan/Smiling>>
            -> No
                <<AddRelationshipScore Stan -10>>
                <<SetSprite Middle Stan/Irritated>> 
                Stan: Fine then! 
                <<SetSprite Middle Stan/Pitiful>>
                Stan: Keep throwing the ball like that and you'll throw the game too.
                Stan: Your loss not mine. Tch. 
                <<SetSprite Middle Stan/Passive>>
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/Smiling>>
            Stan: I get that you're probably here to practice, but why are you coming here so often?
            <<SetSprite Middle Stan/Grin>>
            Stan: Are you trying to get me to fall in love with you or something? 
            -> Yes
                <<SetSprite Middle Stan/Question>>
                <<Shake Middle>>
                Stan: Oh...
                <<SetSprite Middle Stan/Pitiful>>
                Stan: You're joking right? 
                <<SetSprite Middle Stan/Question>>
                Stan: Unless...?
                -> I'm not joking
                    <<AddRelationshipScore Stan 10>>
                    <<Shake Middle>>
                    Stan: Huh? 
                    <<SetSprite Middle Stan/Pitiful>>
                    Stan: Uhh... I mean I get that I'm really tall, strong, and handsome...
                    <<SetSprite Middle Stan/Laughing>>
                    Stan: But what the hell is wrong with you? 
                    Stan: I haven't been anything except mean to you and you're in love with me?
                    <<SetSprite Middle Stan/Grin>>
                    Stan: You're more messed up than I thought. I guess we're kind of similar in that regard.
                    Stan: In all seriousness, we're in a field; go touch grass. 
                -> Haha just kidding! 
                    <<AddRelationshipScore Stan 10>>
                    <<SetSprite Middle Stan/Smiling>>
                    Stan: Heh. You're pretty fun to banter with.
            -> No
                <<SetSprite Middle Stan/Smiling>>
                Stan: Oh really? 
                <<SetSprite Middle Stan/Grin>>
                Stan: You know, I am one of the most popular football players amongst the students here...
                Stan: I guess people have a weak spot for the emotionally unavailable bad boy. 
                <<SetSprite Middle Stan/Pitiful>>
                Stan: Maybe it won't be much longer before you fall for my charms.
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/Question>>
            Stan: It's really important to train both your body and your mind. 
            Stan: I spend a lot of time practicing on the field, but I also hit the books just as hard. 
            <<SetSprite Middle Stan/Grin>>
            Stan: My economics homework isn't going to finish itself. 
            <<SetSprite Middle Stan/Question>>
        <<elseif $Stan_Dialogue == 4>> 
            <<SetSprite Middle Stan/Question>>
            Stan: Even though Tommy and I are friends, we don't have many things in common. 
            <<SetSprite Middle Stan/Blackout>>
            Stan: He invites me to go to a club with him, but the loud music and trashy dancing isn't exactly my taste.
            <<SetSprite Middle Stan/Question>>
            Stan: Though to be fair, I don't think chess is his favorite activity either. 
            <<SetSprite Middle Stan/Giggle>>
            Stan: Last time we were playing chess, he was sleeping in between turns!
            Stan: I started just taking random pieces of his while he was dozing off as a prank and he didn't even realize until his king was missing.
            <<SetSprite Middle Stan/Smiling>>
            Stan: He's a funny guy though and a good friend. 
        <<else>> 
            <<SetSprite Middle Stan/Question>>
            Stan: Do you know Harvey Ford? The real estate magnate? 
            <<SetSprite Middle Stan/Smiling>>
            Stan: He's my dad and runs the family real estate business. One day, I hope to take over the family business. 
            Stan: That's one of the main reasons that I'm studying economics. 
            <<SetSprite Middle Stan/Sweating>>
            Stan: It's pretty tough to impress my old man though.
            <<SetSprite Middle Stan/Smiling>>
        <<endif>>
    <<elseif GetRelationshipScore("Stan") >= 300 && GetRelationshipScore("Stan") < 400>> //Crushing
        <<if $Stan_Dialogue == 1>>
            <<SetSprite Middle Stan/Grin>>
            Stan: You're kind of small for an athlete... but you put in a lot of work. 
            <<SetSprite Middle Stan/GrinBlush>>
            Stan: It's kind of cute actually... how small you are compared to me. 
            <<SetSprite Middle Stan/Smiling>>
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/Question>>
            Stan: I'm about to go on a warmup run if you want to join me. 
            <<SetSprite Middle Stan/SmilingBlush>>
            Stan: We can go at whatver speed you feel comfortable at.
            <<SetSprite Middle Stan/Smiling>>
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/Question>>
            Stan: Did you remember to put on sunblock today? You seem a little red.
            <<SetSprite Middle Stan/GrinBlush>>
            Stan: Or are you just flustered around me? 
            <<SetSprite Middle Stan/Smiling>>
        <<elseif $Stan_Dialogue == 4>> 
            <<SetSprite Middle Stan/Question>>
            Stan: Hey, can you swear that you won't tell anyone about what I'm going to tell you?
            -> I swear!
                <<SetSprite Middle Stan/Passive>>
                Stan: I have an upcoming interschool debate for this one class, but I'm going against Kelly from UCL. 
                <<SetSprite Middle Stan/Question>>
                Stan: She and I have been rivals since elementary school. 
                Stan: Do you know her? The tall-ish super serious glasses-chick from your school? Political science and computer science double major? 
                <<SetSprite Middle Stan/Furious2>>
                Stan: I fear no man... but that girl... she scares me. 
                <<SetSprite Middle Stan/Question>>
                Stan: She just has this dead fish nonchalant stare on her all the time. You can hardly tell what she's thinking. 
                <<SetSprite Middle Stan/Passive>>
                Stan: She gives me the creeps. 
            -> No way!
                <<AddRelationshipScore Stan -20>>
                <<SetSprite Middle Stan/Irritated>>
                <<Shake Middle>>
                Stan: And here I thought that I could start to trust you. 
                <<SetSprite Middle Stan/Furious2>>
                Stan: I guess I thought wrong. 
                <<SetSprite Middle Stan/Passive>>
        <<else>> 
            <<SetSprite Middle Stan/Question>>
            Stan: Say, do you like boba by any chance? 
            -> Yes
                <<SetSprite Middle Stan/Sweating>>
                Stan: I can't say I'm a fan of boba. I could never get used to the texture and the drinks are quite sweet. 
                <<SetSprite Middle Stan/Question>>
                Stan: If you like boba though, I think a new boba place just opened around here. 
                <<SetSprite Middle Stan/GrinBlush>>
                Stan: If you want we can go together...
                <<SetSprite Middle Stan/Smiling>>
                Stan: I probably won't get anything though, but it would be nice to spend some time with you outside of sports practice.
            -> No
                <<SetSprite Middle Stan/Smiling>>
                Stan: Me neither. I can't get used to the texture and the drinks are too sweet for my taste. 
                <<SetSprite Middle Stan/Question>>
                Stan: I would choose a good old-fashioned cup of tea over boba any day of the week. 
                <<SetSprite Middle Stan/Smiling>>
                Stan: I'm glad we're on the same page.
        <<endif>>    
    <<else>> //Lovers
        <<if $Stan_Dialogue == 1>>
            <<SetSprite Middle Stan/Question>>
            Stan: Have you heard of Harvey Ford? 
            Stan: You might know him as the senator, but he's also my dad. 
            Stan: He's quite strict, but I admire him a lot. 
            Stan: He also runs Ford Motor Company with an iron fist. 
            <<SetSprite Middle Stan/Grin>>
            Stan: I want to take over the family business someday. It's part of the reason why I'm studying economics. 
            <<SetSprite Middle Stan/Question>>
            Stan: My old man is pretty old fashioned and tough to impress though, so I definitely have my work cut out for me.
            <<SetSprite Middle Stan/Idle>>
        <<elseif $Stan_Dialogue == 2>>
            <<SetSprite Middle Stan/Giggle>>
            Stan: Your backpack looks pretty heavy. Let me hold it for you!
            -> Alright!
                <<SetSprite Middle Stan/Grin>>
                Stan: There we go! 
                <<SetSprite Middle Stan/GiggleBlush>>
                Stan: See? Don't your shoulders feel a little better now? 
            -> No it's ok!
                <<SetSprite Middle Stan/Question>>
                Stan: You're holding your backpack on one shoulder. It's going to mess up your balance over time. 
                <<SetSprite Middle Stan/GiggleBlush>>
                Stan: Now hand it over before I pick you up with it too!
        <<elseif $Stan_Dialogue == 3>>
            <<SetSprite Middle Stan/Question>>
            Stan: It's pretty hot today... Have you been drinking enough water? 
            <<SetSprite Middle Stan/GiggleBlush>>
            Stan: If you forgot your water bottle, you can take a couple sips from mine.
        <<elseif $Stan_Dialogue == 4>> 
            Stan: I heard that coach is making us do pair exercises for our drills today. 
            <<SetSprite Middle Stan/SmilingBlush>>
            Stan: If you don't have a partner yet, I think we should team up.
            <<SetSprite Middle Stan/Grin>>
            Stan: I don't mind if you're not the strongest or fastest teammate.
            <<SetSprite Middle Stan/GiggleBlush>>
            Stan: It would just be nice to spend some time with you. 
        <<else>> 
            <<SetSprite Middle Stan/Smiling>>
            Stan: I heard your school is hosting a school dance sometime soon. 
            <<SetSprite Middle Stan/Question>>
            Stan: I had my doubts about how fun it could be, but I was looking on social media and they're putting a lot of effort into this. 
            <<SetSprite Middle Stan/Smiling>>
            Stan: The catering company they're hiring is really good and there's going to be a boba bar too. 
            <<SetSprite Middle Stan/Question>>
            Stan: I think the dance committee is still debating on whether or not to buy a bouncy castle. 
            Stan: I'm not sure why they're putting so much money into the dance when your school looks like...that... but it seems fun. 
            <<SetSprite Middle Stan/Giggle>>
            Stan: I know I make fun of you and your school a lot but I'd... umm..
            <<SetSprite Middle Stan/Grin>>
            Stan: This is a little silly but...
            <<SetSprite Middle Stan/GiggleBlush>>
            Stan: I'd really like to go to the dance with you. 
            <<SetSprite Middle Stan/Smiling>>
        <<endif>>  
    <<endif>>
    <<jump Stan>>
//Stan should dislike all gifts
-> Give Gift <<if $GiftStan == false && (GetInventoryLength(false) > 0 || GetInventoryLength(true) > 0)>>
    <<OpenInventory>>
    <<if GetInventoryCanceled() == true>>
        <<jump Stan>>
    <<endif>> 
    <<set $GiftStan = true>>
    <<set $GiftScoreStan = GetItemScore("Stan")>>
    <<AddRelationshipScore Stan {$GiftScoreStan}>>
    <<if $GiftScoreStan == -10>>
        <<if GetRelationshipScore("Stan") >= 200>>
            <<SetSprite Middle Stan/Irritated>>
            Stan: Uhhh.... no thank you.
        <<else>>
            <<SetSprite Middle Stan/Laughing>>
            Stan: HA! What the hell is this?
            <<SetSprite Middle Stan/Pitiful>>
            Stan: If you think you can win me over with gifts, you're mistaken.
        <<endif>>
        <<SetSprite Middle Stan/Passive>>
    <<else>>
        // Not their key item gift
        Stan: I think that belongs to someone else... 
        <<if $GiftScoreK == 0>> // Ellie's
            <<AddItem 40 0 0 0 0 0 "Gifts/Cookbook" "Cookbook" true>>
        <<elseif $GiftScoreK == 1>> // Stan's
            <<AddRelationshipScore Stan -1>>
            <<AddItem 1 40 1 1 1 1 "Gifts/Diary" "Diary" true>>
        <<elseif $GiftScoreK == 2>> // Santana's
            <<AddRelationshipScore Stan -2>>
            <<AddItem 2 2 40 2 2 2 "Gifts/Spade" "Spade" true>>
        <<elseif $GiftScoreK == 3>> // Riviera's
            <<AddRelationshipScore Stan -3>>
            <<AddItem 3 3 3 40 3 3 "Gifts/ToteBag" "ToteBag" true>>
        <<elseif $GiftScoreK == 4>> // Irene's
            <<AddRelationshipScore Stan -4>>
            <<AddItem 4 4 4 4 40 4 "Gifts/Keychain" "Keychain" true>>
        <<else>> // Tommy's
            <<AddRelationshipScore Stan -5>>
            <<AddItem 5 5 5 5 5 40 "Gifts/Watch" "Watch" true>>
        <<endif>>
    <<endif>>
    <<jump Stan>>
-> Ask <<if $AskStan == false>>
    -> Ask on a date
        <<set $AskStan = true>>
        <<if GetRelationshipScore("Stan") >= 300>>
            <<AddRelationshipScore Stan 10>>
            <<if GetRelationshipScore("Stan") >= 300 && GetRelationshipScore("Stan") < 400>>
                <<SetSprite Middle Stan/Question>>
                Stan: A date? with you?
                <<SetSprite Middle Stan/Grin>>
                Stan: Fallen for my charms, haven't you? 
                Stan: Sure, why not. Let's see where this goes.
            <<else>>
                <<SetSprite Middle Stan/Grin>>
                Stan: A date? I'd love to. 
                Stan: Choose any place you'd like but let me take care of the bill this time alright?
            <<endif>>
            <<Fade false>>
            <<ClearSprite Middle>>
            <<SetActiveChar Date>>
            <<SetBackground Backgrounds/Date>>
            <<SetSprite Bottom Backgrounds/Table>>
            <<SetSprite Date Stan/Smiling>>
            <<Fade true>>
            <<Wait 1>>
            <<if LoadInt("FirstDate") == 0>>
                <<SetActiveChar Right>>
                <<SetSprite Right BearBox/Idle>>
                BearBox: Pssst! Hey buddy!
                BearBox: I heard it was your first date, so I rolled over as fast as I could to give you some last minute tutorial advice. 
                BearBox: Just like chocolate, there are a lot of ways that you can mess up a date!
                BearBox: Like adding raspberry syrup. 
                <<Wait 1>>
                <<Flip Middle 0>>
                <<Flip Middle 0>>
                <<Wait 1>>
                BearBox: Pretend that made sense. 
                BearBox: Anyways, navigate your feelings using the WASD or arrow keys to get to the heart end goal. 
                BearBox: Don't screw up your date by doing something out of place, like putting raspberry syrup on chocolate. 
                BearBox: Y'know what this metaphor doesn't make sense. 
                BearBox: Raspberry syrup sucks, especially when it gets in my tires, so avoid the red bars and your date should go well. 
                <<SetSprite Right BearBox/Blush>>
                BearBox: That's all from me! Good luck on your date champ! Stay out of trouble!
                <<ClearSprite Right>>
                <<SaveInt "FirstDate" 1>>
                <<SetActiveChar Date>>
            <<endif>>
            <<PlayMusic Music/Minigame>>
            <<DateMinigame>>
            <<PlayMusic Music/Gameplay>>
            <<Wait 1>>
            <<if DateMinigameWon()>>
                <<SetSprite Date Stan/Smiling>>
                Stan: I usually don't go to restaurants like these. My family always eats at the same famous restaurants. 
                <<SetSprite Date Stan/Giggle>>
                Stan: The restaurant interior isn't the most impressive, but the food is really good! 
            <<else>>
                <<SetSprite Date Stan/Irritated>>
                <<Shake Date>>
                Stan: God, you're getting food everywhere. Stop eating like a slob, you're embarassing me.
                <<SetSprite Date Stan/Blackout>>
                Stan: I hope no one sees me in public like this...
            <<endif>>
            <<Fade false>>
            <<ClearSprite Bottom>>
            <<ClearSprite Date>>
            <<if $TimeofDayField == 0>>
                    <<SetBackground Backgrounds/Field/Morning>>
            <<elseif $TimeofDayField == 1>>
                    <<SetBackground Backgrounds/Field/Noon>>
            <<else>>
                    <<SetBackground Backgrounds/Field/Night>>
            <<endif>>
            <<if DateMinigameWon()>>
                <<SetSprite Middle Stan/Smiling>>
                <<SetActiveChar Middle>>
                <<Fade true>>
                <<SetSprite Middle Stan/GiggleBlush>>
                Stan: It's nice to try something different and local. 
                Stan: Time and time again, you never fail to surprise me. 
                <<SetSprite Middle Stan/GrinBlush>>
                Stan: I really enjoyed this date. Thank you. 
                <<SetSprite Middle Stan/Smiling>>
                <<AddRelationshipScore Stan 15>>
            <<else>>
                <<SetSprite Middle Stan/Irritated>>
                <<SetActiveChar Middle>>
                <<Fade true>>
                Stan: Look, I like you, but have got to have better table manners if we're going to be seen in public together. 
                <<SetSprite Middle Stan/Passive>>
                Stan: Maybe go to the library and pick up a book on table etiquitte. 
                <<AddRelationshipScore Stan -10>>
            <<endif>>
            <<jump Stan>>
        <<else>>
            <<SetSprite Middle Stan/Question>>
            Stan: You? Me? A date together? 
            Stan: That's hilarious. Why the hell would I ever do that? 
            <<SetSprite Middle Stan/Laughing>>
            <<jump Stan>>
        <<endif>>
    -> Ask to the dance
        <<set $AskStan = true>>
        <<if GetRelationshipScore("Stan") >= 400>>
            Player: Would you like to go to the dance with me?
            <<SetSprite Middle Stan/Question>>
            Stan: The dance? 
            <<SetSprite Middle Stan/GrinBlush>>
            Stan: I would be honored. 
            <<set $DancePartner = 6>>
            <<jump Dance>>
        <<elseif GetRelationshipScore("Stan") < 400 || GetRelationshipScore("Stan") >= 200>>
            <<SetSprite Middle Stan/Question>>
            Stan: I don't care enough about you right now to warrant the social embarassment of going to that dinky old dance.
            <<AddRelationshipScore Stan -5>>
            <<jump Stan>>
        <<else>>
            <<SetSprite Middle Stan/Question>>
            Stan: You? Me? That dinky old dance?
            <<SetSprite Middle Stan/Pitiful>>
            Stan: I respect your little attempt to swindle me into your little fundraiser project, but I'd never be caught dead stepping foot into there. 
            Stan: With YOU nonetheless!
            <<SetSprite Middle Stan/Laughing>>
            Stan: That's the most hilarious thing anyone has said to me this century!
            <<SetSprite Middle Stan/Passive>>
            <<AddRelationshipScore Stan -10>>
            <<jump Stan>>
        <<endif>>
    -> Cancel
        <<jump Stan>>
-> Leave
    <<UpdateTimeOfDay>>
    <<LoadScene MapScene>>
===