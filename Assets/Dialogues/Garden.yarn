title: Garden
tags:
position: -107,2
---
<<set $TimeOfDay to GetTimeOfDay()>>
<<if $TimeOfDay == 0>>
    <<FadeBackground Backgrounds/Garden/Morning 0>>
    //Sets background as Garden Morning if it is Daytime
<<elseif $TimeOfDay == 1>>
    <<FadeBackground Backgrounds/Garden/Noon 0>>
    //Sets background as Garden Afternoon if it is Afternoon
<<else>>
    <<FadeBackground Backgrounds/Garden/NightWarm 0>>
    //Sets background as Garden Night if it Evening
<<endif>>
-> Meet someone
-> Take a Walk
    <<if $TimeOfDay == 0 || $TimeOfDay == 1>>
        <<jump RandomEvent>>
    <<else>>
        <<jump USC>>
    <<endif>>  
-> Leave
    <<LoadScene MapScene>>
===
title: USC
position: 210,112
---
<<set $TalkTommy to false>>
<<set $GiftTommy to false>>
<<set $AskTommy to false>>
<<SetSprite Middle Tommy/Idle>>
<<SetActiveChar Middle>>
Player: What should I do?
-> Talk to Tommy <<if $TalkTommy == false>>
    <<set $TalkTommy to true>>
    <<AddRelationshipScore Tommy 10>>
    <<set $USC_Dialogue = Dice(5)>>
    //10 points will be added to the relationship score for just talking to USC. 
    //Different lines from different dialogue "pools" will be pulled depending on the player's 
    //relationship with the character. There are 5 levels of relationships: Strangers, Acquaintances,
    //Friends, Crushing, and Lovers. Normally, these are in increments of 100 (ie Strangers = 0-99)
    //but since USC is "rare" and shows up at night, I decided to lower his standards. 
    <<if GetRelationshipScore("Tommy") < 80>>
        // Strangers
        <<if $USC_Dialogue == 1>>
            Tommy: Be careful not to get your... dirt... on my Bucci shoes. 
        <<elseif $USC_Dialogue == 2>>
            Tommy: Oh sorry, I don't have cash on me.
        <<elseif $USC_Dialogue == 3>>
            Tommy: Can I get some security backup here?
        <<elseif $USC_Dialogue == 4>> 
            Tommy: What an... interesting... choice of an outfit...
        <<else>> 
            Tommy: Looking for a fight or something?
            Tommy: I'd totally take you on but uh... I don't want to scuff my Bucci watch. 
        <<endif>>
    <<elseif GetRelationshipScore("Tommy") >= 80 && GetRelationshipScore("Tommy") < 160>>
        //Acquaintances
        <<if $USC_Dialogue == 1>>
            Tommy: Oh it's you again. You seem to come here often. 
        <<elseif $USC_Dialogue == 2>>
            Tommy: Y'know? You're not so bad for a public school kid.
                -> Thanks?
                    Player: Thanks?
                    Tommy: You're welcome!
                    Tommy: Our business school is objectively better though, and you can pay your tuition with a credit card, so your tuition is free! 
                    Player: ...
        <<elseif $USC_Dialogue == 3>>
            Tommy: Do you like taking night time walks too?
                -> Yeah!
                    Tommy: Me too. It's nice to take a break from the city life once in a while. 
                -> I just come here to see you. 
                    Player: To be honest, I just come here to see you.
                    Tommy: Uhhhhhhhhhhhhhhhhh.... did you mean to click on this option?
                    Tommy: Creep. 
                -> No, I just came here to fight you!
                    <<AddRelationshipScore Tommy -20>>
                    Player: No, I just came here to fight you!
                    Tommy: I'd totally take you on but uh... 
                    Tommy: I'm carrying... eggs... in my....pocket... 
                    Tommy: ...AND I DON'T WANT THEM TO BREAK!!
                    Tommy: ...so maybe another day...
        <<elseif $USC_Dialogue == 4>> 
            Tommy: Sometimes you can see the squirrels do funny things at night.
            Tommy: One time I was eating and a squirrel started yelling at me to give it food. You have to resist feeding them though. 
            Tommy: The writer of this dialogue sequence got a nasty squirrel bite trying to feed one of those bastards. 
            Tommy: It made her bleed a ton and she still has the scar.
            Tommy: But I digress.
        <<else>> 
            Tommy: To be honest, I don't really know why we still have national debt.
            Tommy: Can't the government just print more money or something? That's what I would do if I was president.
            Tommy: Maybe I should run for president after college! I'll make my own party! I'll call it the "Deez Nuts" party!
        <<endif>>
    <<elseif GetRelationshipScore("Tommy") >= 160 && GetRelationshipScore("Tommy") < 240>>
        // Friends
        <<if $USC_Dialogue == 1>>
            Tommy: Living a fast-paced life is nice and all, but sometimes things feel kind of overwhelming or complicated.
            Tommy: Majoring in business can get pretty tiring at times. 
            Tommy: It's kind of nice to come out here and clear my head, y'know?
        <<elseif $USC_Dialogue == 2>>
            Tommy: My family and friends always told me to stay away from the common folk.
            -> Why's that?
                Player: Why's that?
                Tommy: They always told me that people outside of our social circle were uncouth and unambitious.
                Tommy: I'm starting to think that they're wrong though. 
                Tommy: I really like the conversations that I have with you.
            -> Your social circle sucks!
                <<AddRelationshipScore Tommy -20>>
                Player: Your social circle sucks!
                Tommy: Hey, I don't agree with all their ideas but I have a lot of respect for them...
                Tommy: Maybe they were right about some things. Geez. 
            -> What do you think? 
                Player: What do you think?
                Tommy: I don't know, I feel like they could be wrong. 
                Tommy: They always told me that people outside of our social circle were uncouth and unambitious.
                Tommy: I'm starting to think that they're wrong though. 
                Tommy: I really like the conversations that I have with you.
        <<elseif $USC_Dialogue == 3>>
            Tommy: What if we were all just living in a simulation or something?
            Tommy: Like just think about all of the philosophical implications of that!
            Tommy: Like does it even matter that I'm grinding my ass off in my business major?
            Tommy: What if my whole personality is just code made by some weird UCLA kid based off of school stereotypes?
            Tommy: Wouldn't that be weird?
        <<elseif $USC_Dialogue == 4>> 
            Tommy: Your outfit's not bad today. I think your sense of style is improving!
        <<else>> 
            Tommy: Check out my new shoes! 
            Tommy: They're the new Bucci x Bupreme collaboration!
            Tommy: Usually, you have to wait in line for like five hours for a chance at buying these.
            Tommy: But I just bribed a pleb to wait in line for me.
            Tommy: Work smart not hard. Business major mindset. 
        <<endif>>
    <<elseif GetRelationshipScore("Tommy") >= 240 && GetRelationshipScore("Tommy") < 320>>
        // Crushing
        <<if $USC_Dialogue == 1>>
            Tommy: It's kind of chilly tonight.
            Tommy: Are you cold?
                -> Yeah...
                    <<AddRelationshipScore Tommy 10>>
                    Player: Yeah... 
                    Tommy: Oh! Do you want to borrow my jacket?  
                        -> Take the jacket.
                            Player: Thanks! 
                            Tommy: Don't mention it. Don't worry about washing it or returning it! I have plenty.
                            Tommy: ...Besides you look really good with it on. 
                        -> Turn him down. 
                            Player: Oh no, I'm good!
                            Tommy: Oh ok. But if you catch a cold don't spread it to me!
                -> Not really. 
                    Player: Not really.
                    Tommy: Oh ok. But if you catch a cold don't spread it to me!
        <<elseif $USC_Dialogue == 2>>
            Tommy: I made a lot of random businesses when I was growing up.
            Tommy: I remember for one of them I would charge people $5 to generate a password for them.
            Tommy: My parents' friends would all support my business too, which is kind of weird because now I know the password of the governor's email.
            Tommy: I wonder if he still uses that same password... that'd be a state security crisis wouldn't it?
            Tommy: What if we tried logging into his email? We could reverse the whole merging thing of the UC schools... if you want...
            Tommy: I'd really miss seeing you around at night though, but I want you to be happy.
                -> Let's do it!
                    Tommy: Just give me a minute to punch in his login information... done!
                    Tommy: Hey...Are you sure you want to do this?
                        -> Yes! I can't wait to get back to how things were before all of this mess!
                            Tommy: Alright... I logged in! I'm sending out an executive order to reverse the UC consolidation. 
                            Tommy: Ok... it's done! I guess starting tomorrow, you can start going to class normally.
                            Tommy: I'm not sure why, but I'll really miss seeing you around!
                            Tommy: I can't seem to shake the feeling that we were supposed be something more though...
                            Tommy: Take care alright? Goodbye...
                            <<FadeBackground BadEndClassroom 5>>
                            Player: I can't believe that Tommy was actually able to deconsolidate the UCs! 
                            Player: It feels great to go back to school normally again... but I can't help but feel incomplete for some reason...
                            <<Fade false>>
                            <<LoadScene UiScene>>
                        -> Actually, this might not be a good idea...
                            Player: Actually, this might not be a good idea...
                            Tommy: Yeah... we could get in major trouble for this... the kind my family probably can't bail me out of...
                -> We probably shouldn't hack into the governor's email...
                    Player: We probably shouldn't hack into the governor's email...
                    Tommy: Yeah...you're right. We could get in major trouble for this... the kind my family probably can't bail me out of...
                    Tommy: More days to spend with you I suppose!
        <<elseif $USC_Dialogue == 3>>
            Tommy: Sorry I was kind of an ass to you when we first met.
            Tommy: I guess I grew up pretty sheltered...but still, that doesn't excuse my behavior. 
            Tommy: I'm sorry... I hope that I can make it up to you. 
                -> It's alright! Just treat me out for food some time alright?
                    <<AddRelationshipScore Tommy 10>>
                    Player: It's alright! Just treat me out for food some time alright?
                    Tommy: Yeah! Of course!
                -> Yeah that was pretty awful of you...
                    Player: Yeah... that was pretty awful of you...
                    Tommy: It was... I can't help but beat myself up thinking about how hurtful it must have been to you. 
        <<elseif $USC_Dialogue == 4>> 
            Tommy: Y'know, I wouldn't mind going out sometime with you... maybe to some place nice. 
        <<else>> 
            Tommy: I saw this sweater at the Bucci store the other day.
            Tommy: It had a bear on it and it reminded me of you. 
            Tommy: Anyways, I bought it for you. 
            Tommy: Oh it's not that expensive or anything. It was on sale for like $2000.
            Tommy: Nothing major. I hope you like it though...
        <<endif>>
    <<else>>
        //Lovers
        <<if $USC_Dialogue == 1>>
            Tommy: I was going through my closet this weekend and I was thinking that I have a ton of clothes.
            Tommy: I used to be really obsessed with collecting designer fashion.
            Tommy: But lately I find more happiness volunteering or spending quality time with my friends or family...
            Tommy: and with you...
            Tommy: What do you think that I should do with all the clothes I don't wear?
                -> Keep them! You spent a lot of money on them!
                    Tommy: Ah, yeah... I did spend a lot of money on them...
                    Tommy: The piles kind of stress me out though...
                -> Donate them!
                    <<AddRelationshipScore Tommy 10>>
                    Tommy: That sounds like a good idea!
                    Tommy: There's this refugee shelter that I've been looking into.
                    Tommy: They could really use some clothes donations...
                    Tommy: Yeah! I think I'll donate the clothes to them!
                -> Sell them and donate the money to charity!
                    <<AddRelationshipScore Tommy 10>>
                    Tommy: That sounds like a good idea!
                    Tommy: There's this non-profit summer school for underprivileged kids that I've been looking into.
                    Tommy: They could really use some new books and school supplies...
                    Tommy: Yeah! I think I'll sell the clothes and donate the money to them!
                -> Give them to me!
                    <<AddRelationshipScore Tommy -20>>
                    Tommy: I guess I could...
                    Tommy: ... but there's this charity I was looking at that I think could better use them.
        <<elseif $USC_Dialogue == 2>>
            Tommy: I'm in this club that tutors high school kids and the whole experience is really changing my world view. 
                -> I hope you aren't tutoring in economics Mr. Money Grows on Trees!
                    Player: I hope you aren't tutoring him in economics Mr. Money Grows on Trees!
                    Tommy: Hey! I know money doesn't grow on trees! 
                    Tommy: Supply and demand confuses me... and I'm still not sure how a credit card works, but that was kind of mean!
                    Tommy: I'm tutoring him in English and public speaking, for your information...
                -> What's that like?
                    Tommy: Oh! It's pretty cool! I tutor kids in English and in public speaking. 
                    Tommy: There's this one kid that comes from a more disadvantaged family, but he works really hard and he's really smart.
                    Tommy: It made me think about how lucky I am to be born off in a well-off family...
                    Tommy: My parents hired tutors for me and paid for a lot of my extracurriculars.
                    Tommy: I'm a lot more grateful for what I have. I think I still want to do something more for people like my tutee though.
        <<elseif $USC_Dialogue == 3>>
            Tommy: 
        <<elseif $USC_Dialogue == 4>> 
            Tommy: 
        <<else>> 
            Tommy: 
        <<endif>>
    <<endif>>
    <<jump USC>>
-> Give Gift <<if $GiftTommy == false && (GetInventoryLength(false) > 0 || GetInventoryLength(true) > 0)>>
    <<OpenInventory>>
    <<if GetInventoryCanceled() == true>>
        <<jump USC>>
    <<endif>> 
    <<set $GiftTommy = true>>
    <<set $GiftScore = GetItemScore("Tommy")>>
    <<AddRelationshipScore Tommy {$GiftScore}>>
    <<if $GiftScore == 20>>
        //Loved Gift
        Tommy: Why thank you! I love these!
    <<elseif $GiftScore == 10>>
        //Liked Gift
        Tommy: Not bad. You've got good taste.
    <<else>>
        //Hated Gift
        Tommy: Ick… did you pick these off the clearance rack?
    <<endif>>
    <<jump USC>>
-> Ask <<if $AskTommy == false>>
    -> Ask on a date
        <<set $AskTommy = true>>
        <<if GetRelationshipScore("Tommy") >= 240>>
            <<jump TommyDate>>
        <<else>>
            Tommy: Uhhh... ew...
            <<jump USC>>
        <<endif>>
    -> Ask to the dance
        <<set $AskTommy = true>>
        <<if GetRelationshipScore("Tommy") >= 400>>
            Player: Would you like to go to the dance with me?
            Tommy: I would love to!
        <<else>>
            Tommy: Uhhhh... no thanks.
            <<AddRelationshipScore Tommy -20>>
        <<endif>>
    -> Cancel
        <<jump USC>>
-> Leave
    <<UpdateTimeOfDay>>
    <<LoadScene MapScene>>
===
title: RandomEvent
position: 204,-37
---
//<<set $Random_Event_Dialogue to RandomRange (1,20)>>
<<set $Random_Event_Dialogue to 18>>
    <<set $TimeOfDay to $TimeOfDay + 1>>
<<if $Random_Event_Dialogue == 1>>
    <<AddMoney 200>>
    Player: Woah! I found $200 on the ground! Sweet!
<<elseif $Random_Event_Dialogue == 2>>
    Player: Woah! I found $50 on the ground! Sweet!
    <<AddMoney 50>>
<<elseif $Random_Event_Dialogue == 3>>
    Player: I just heard a voice telling me that Kelly loves sheet masks...
    Player: Makes sense since she's stressed all the time...
    Player: She's in the library late at night. 
<<elseif $Random_Event_Dialogue == 4>>
    Player: I just heard a voice telling me that Irene loves B-Fuel...
    Player: That kind of fits with her gamer personality...
<<elseif $Random_Event_Dialogue == 5>>
    Player: I just heard a voice telling me that Santana loves plushies...
    Player: She's kind of a soft girl... I can see her loving plushies. 
<<elseif $Random_Event_Dialogue == 6>>
    Player: I just heard a voice telling me that Riviera loves stationery...
    Player: She's really hardwording... I can see her running out of notepads easily.
<<elseif $Random_Event_Dialogue == 7>>
    Player: I just heard a voice telling me that Ellie loves flowers...
    Player: She seems to like nature a lot... so it makes sense. 
<<elseif $Random_Event_Dialogue == 8>>
    Player: I have a feeling that if I take a walk at night, I'll meet someone unexpected. 
<<elseif $Random_Event_Dialogue == 9>>
    Player: Dang it! The squirrel took $50 from me when I wasn't looking. 
    Player: Literally what does he even need it for?!
    <<AddMoney -50>>
<<elseif $Random_Event_Dialogue == 10>>
    Player: Huh... a squirrel...
    Player: Oh?! He's giving me $100.
    Player: Thanks buddy! I'll give you some GFuel sometime. 
    <<AddMoney 100>>
<<elseif $Random_Event_Dialogue == 11>>
    Player: I just heard a voice telling me something about a person wearing red and liking Bucci wallets.
    Player: I must be sleep deprived or something.
<<elseif $Random_Event_Dialogue == 12>>
    //UCB special item
    //NOTE: Finalize special items then add AddItem commands after "Oh!"
    Player: Oh! I found a diary! It looks special... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 13>>
    //UCLA special item
    Player: Oh! I found a recipe book! It looks special... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 14>>
    //UCR special item
    Player: Oh! I found a pen! It looks special... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 15>>
    //UCI special item
    Player: Oh! I found a key chain! It looks special... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 16>>
    //UCSB special item
    Player: Oh! I found a handwritten poem book! It looks special... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 17>>
    //USC special item
    Player: Oh! I found a watch! It looks expensive... I wonder who it belongs to...
<<elseif $Random_Event_Dialogue == 18>>
    <<SetSprite Middle Squirrel/Idle>>
    Player: Oh! A squirrel! Hey there buddy!
    <<Shake Middle>>
    Player: Oh? You're approaching me? 
    <<SetSprite Middle Squirrel/Menacing>>
    <<Shake Middle>>
    Player: Instead of running away, you're coming right to me...
    Squirrel: ...
    Player: ...?
    <<SetSprite Middle Squirrel/Idle>>
    <<Shake Middle 999 60>>
    Squirrel: !!!
    Player: OH MY GOD! HELP ME!!! HOLY SHIT!!! I'M BEING ATTACKED BY A SQUIRREL!!!!
    <<ClearSprite Middle>>
    Player: .... owww... that hurt a lot more than I anticipated...
    Player: AND HE TOOK $50??? WHY!!!
    <<AddMoney -50>>
<<elseif $Random_Event_Dialogue == 19>>
    <<SetSprite Middle Squirrel>>
    Player: Oh! A squirrel! 
    Player: Hey there buddy!
    Squirrel: Hey.
    Player: Wait did that squirrel just TALK??
    <<ClearSprite Middle>>
    Player: Wait where'd it go? Wait, why do I have $300 more dollars in my pocket?
    Player: I swear there was a squirrel here just now...
    Player: I need to get more sleep...
    <<AddMoney 300>>
<<else>>
    Player: DAMMIT! I sprained my ankle walking near Paines Hall. 
    Player: Better book a urgent care appointment...
    Player: Looks like my pockets are going to be $100 lighter.
    PLayer: I wish I went to a private school. 
    <<AddMoney -100>>
<<endif>>
-> Leave
    <<UpdateTimeOfDay>>
    <<LoadScene MapScene>>
===